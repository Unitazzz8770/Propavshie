
VAR nomer = 0
VAR rodaki = 0
VAR v_gorode_proshli = 0
VAR respekt_nini = 0
VAR foto = 0
VAR babuskina_pomosh = 0
VAR krovat_smotrim = 0
VAR kamera_zariadka = 0
->nachalo

===nachalo===
#Location: OdeialaNochiuIdut
22 декабря...
Ночь. Странные белые существа шагают по улицам города. Их силуэты, не издавая звуков, вереницей бредут в одном направлении. Город окутала зловещая атмосфера. 
Снег падает хлопьями. 

Какое-то время назад...

20 декабря, 18:00.
#Location: Nachalo
->nachalo.uchim_nomer

=uchim_nomer

Мама: Окей, Саш. Давай заново! #Н

Ты: 100... эээ... 6666. #П

Мама: Саш, 112! 112! Номер службы спасения обязательно нужно знать!.. #Н

Мама: Саш, мы с папой через несколько дней уедем по работе. #Н

Мама: Ты поживешь у бабушки и, если ты будешь знать номер спасателей, мне будет намного спокойнее. Понимаешь, да? #Н

Из кухни распространялся запах гари. Мама втрепенулась и взглянула в сторону двери.

Мама: Ой-ой-ой! Кажется каша убежала! #Н

Мама скрылась в проходе, а ты осталась наедине с номером. 

Конечно, очень важно знать номер службы спасения, но это можно было бы сделать и позже! 

Может быть стоит пока отдохнуть и пойти смотреть телевизор?

* [Доучить номер.]
 ~ nomer = 1
Нехотя, ты решила доучить номер. На всякий случай!
Несколько минут спустя

Ты: 102.. 102.. 102.. 102.. вроде все! Ура! Мультики! #Р

Номер надолго запомнился тебе. Теперь то точно его не забудешь. 
-> multiki

* [Мультики! Мультики! Мультики!]
Поучить можно и позже, а сейчас самое время для мультиков! 
 
"Грамотный отдых - залог успеха!" - как говорит сама Саша. 
-> multiki

===multiki===
Ты уселась на диван и включила детскую телепрограмму. Мама принесла подгоревшую кашу в твоей любимой тарелке.

Ближе к вечеру пришел папа и вы вместе поиграли в настольные игры. 

А потом все отправились спать. 

Ты почти сразу погрузилась в сон.
->propalo_odeialo

===propalo_odeialo===
#Location: My_room

Но спокойной выспаться не получилось.

21 декабря, 02:10...

Ты проснулась в районе двух часов ночи и с полузакрытыми глазами встала с кровати. 

Тебя трясло от холода. По телу бегали мурашки. 

Через мгновение ты осознала, что твое одеяло куда-то пропало, а окно приоткрыто!

Ты: Где мое одеяло? Что происходит? #У

Послышался сигнал корабля со стороны моря. Ничего не хотелось делать, ты закрыла окно. 

От холода сон сняло рукой, но думалось с трудом.
->noch1_cto_cdelat

===noch1_cto_cdelat===
#Location: Coridor

Ты вышла в коридор.

Что стоит сделать?

+[Проверить кровать.]
->My_room

+[Пойти в комнату родителей.]
->noch1_cto_cdelat.komnata_rodakov
 
=My_room
#Location: My_room
На кровати не было ничего подозрительного. 
 
Если не считать пропажу одеяла чем-то странным, то все было вполне обычно.
->noch1_cto_cdelat

=komnata_rodakov

Ты прошла по коридору и заглянула в комнату родителей. 
 
Их одеяло тоже пропало.
 
Случайностью это быть не может. 
->u_krovati_roditelei

===u_krovati_roditelei===
Ты подошла к кровати родителей и задумалась. 

Стоит ли будить родителей? 

*[Пусть спят.]

Подумав, что родители будут только мешать, ты решила провести собственное расследование.'
->ostatoc_pervoi_nochi
 
*[Мама! Папа! Где все одеяла?!]

~ rodaki = 1
Ты включила ночник и подошла к кровати. 
 
Родители приоткрыли глаза и, в полусне, вопросительно посмотрели на тебя.

Мама: Что?.. Саша? А где наше одеяло? #Н

Ты: Я не знаю. Мое одеяло тоже пропало! #З

Папа: Пойдемте проверим в шкафу. У нас, кажется, были запасные. #Н
->ostatoc_pervoi_nochi
===ostatoc_pervoi_nochi===
#Location: Coridor

Что стоит сделать?

+[Проверить свою кровать.]
#Location: My_room
Ты снова решила проверить свою кровать. 
 
Заглянув в комнату, одеяла ты не нашла.
 
Может быть, если проверить еще несколько раз, оно появится?
->ostatoc_pervoi_nochi
+[Пойти в комнату родителей.]

На всякий случай, ты решила проверить комнату родителей. 
 
Удивительно, но одеяла на их кровати не оказалось! Интересно почему..
->ostatoc_pervoi_nochi
 
*[Посмотреть в окно.] 
->ostatoc_pervoi_nochi.okno


+[Поискать одеяла.]

{rodaki == 0: Я не знаю где лежат одеяла! Обычно их мама достает! }

{rodaki == 1: Вы проверили шкаф. В нем одеял не было. Впрочем... Не удивительно! }
{rodaki == 1: Зато мама нашла несколько подушек и покрывал. }
->ostatoc_pervoi_nochi

+[Пойти спать без одеял.]

#Location: My_room
Больше в голову ничего не приходило и ты, одевшись потеплее, отправилась спать. 
 
Обрывки мыслей бегали по кругу. 
 
Что происходит? Куда делись все одеяла? Что будет дальше? 
 
Наконец, ты заснула.
->s_dobrim_utrom
 
=okno
#Location: Okno1
Войдя в свою комнату, ты бросаешь заинтересованный взгляд на...

#Location: Okno2
Окно... 

Ты смотришь в окно. В темноте почти ничего не видно. 

#Location: Okno3
Только круглая луна освещает ночные улицы. 

Вдруг, ты заметила как какая-то загадочная, белая фигура идет по улице. 

На человека этот кто-то не похож...

Стало жутковато, но так или иначе...

Что стоит сделать?

*[Закрыть окно, пока белый незнакомец тебя не увидел!]
#Location: Okno1
Было слишком страшно, чтобы что-то сделать! 

Закрыв и занавесив окно, ты поняла, что лучше обходить окна стороной! 

*[Сделать фото, пока оно не убежало!]
~ foto = 1
Ты медленно потянулась за телефоном, стараясь не сводить взгляда с фигуры. 
Незнакомец начал скрываться в темноте, ты подбежала к окну. 
#Location: Okno1
Фотография получилась. Захлопнув и занавесив окно, ты села на кровать. 
Как на зло, телефон в этот момент разрядилась. Придется искать зарядку, чтобы посмотреть. Сегодня не твой день.
-
->ostatoc_pervoi_nochi
 
===s_dobrim_utrom===
21 декабря, 9:00...

Утренний свет пробрался в твою комнату. Одеяло было на месте!
Ты настолько удивилась, что моментально раскрыла глаза и несколько раз потрогала его.
Оно было вполне реальным! 

Одеяло лежало так, будто бы ничего и не произходило этой ночью.

#Location: Smotrim_telek
Прибежав на кухню, ты взяла завтрак и пошла смотреть телевизор.

Ты уселась на диван, укуталась в плед и включила первый попавшийся телеканал. 

Ведущий: К слову, в городе Н сегодня произошло странное происшествие! #Н
Ведущий: У нескольких людей пропали одеяла!  Самое интересное, что утром одеяла вернулись! #Н

Ведущий: Сейчас вы увидите репортаж с места действия!! #Н

На экране появилась бабушка, стоящая на фоне небольшого продуктового магазина.

На нем красовалась вывеска "Магазин 24 часа".

Баба Надя: Мы с девочками думаем, что все не просто так! #Н
Баба Надя: А Нина Павловна не спала, когда одеяла пропали, и все видела!.. #Н

Баба Надя: ... #Н

Баба Надя: О! А вот и она, кстати! #Н

#Location: Interviu2
Баба Надя: Нина, расскажи молодому человеку про одеяло свое! #Н

Баба Нина: Так.. Сюда говорить надо? Так вот, у меня бессоница была и я видела, как мое одеяло убежало на улицу! #Н

#Location: Smotrim_telek
Ведущий: Эээ... Как интересно... хе-хе... перейдем к следующим новостям! В Самаре школьни... #Н

Ты: То есть одеяло пропало не только у меня... Нужно найти этих бабуль! #З

#Location: Vihodim_iz_doma
Ты: А может не только бабушки заметили, что-то странное. #Р

Ты предвкушала будущее приключение! 
Всегда хотелось чего-то большего, чем простых детских игр в сыщиков! 
Ты ждала этого момента и упустить его никак нельзя! 
#Location: Dumaem_cuda
Натянув куртку и выбежав на улицу, ты задумалась...
->begaem_po_gorodu

===begaem_po_gorodu===

Куда идем?

*[В порт!]
->begaem_po_gorodu.port

*[К бабушке!]
->begaem_po_gorodu.babushka

+[В полицейский участок!]
{v_gorode_proshli == 3: ->begaem_po_gorodu.polis}

{v_gorode_proshli != 3 and v_gorode_proshli != 4: Пойдем туда позже... Сейчас не время!}
{v_gorode_proshli != 3 and v_gorode_proshli != 4: ->begaem_po_gorodu}

{v_gorode_proshli == 4: Посмотри на время! Уже темнеет и нужно домой.!}
{v_gorode_proshli == 4: ->begaem_po_gorodu}

+[Пора домой!]
{v_gorode_proshli == 4: Ты уже устала, наступил вечер и пора возвращаться.}
{v_gorode_proshli == 4: ->noch2}

{v_gorode_proshli != 4: Мне кажется, ты что-то упускаешь. Еще есть время!}
{v_gorode_proshli != 4: ->begaem_po_gorodu}

= polis
#Location: idem
~ v_gorode_proshli = v_gorode_proshli + 1
Ты направляешься в полицейский участок! 
Перед тобой возникло небольшое сине-белое здание. 
Твой дядя работает тут. Надеюсь, что здесь найдется что-то любопытное!

Войдя внутрь, ты находишь дядю Юру сидящим за своим столом с каким-то приборчиком в руках. 

Ты: Здраствуйте, дядя Юра! #Р

Дядя Юра: Привет, Саш. Ты что-то хотела? #Р

Ты: Просто проведать хотелось. А вы чем занимаетесь? #Н

Дядя Юра: А мне какой-то новый гаджет прислали, а как им пользоваться не сказали. Разобраться пытаюсь. #Н

Ты видишь странный обруч с маленьким экранчиком. 

->begaem_po_gorodu.pribor

=pribor
Что думаешь?

+[Посмотреть инструкцию.]
На столе лежит инструкция к прибору. 

Текст написан на китайском. 

Жалко, что ты его не знаешь.
->begaem_po_gorodu.pribor

+[Осмотреть комнату.]
Осмотрев комнату, ты увидела собаку! 

Она сидела так тихо, что ты ее не замечала! Вот это выдержка...
->begaem_po_gorodu.pribor

+[Все понятно!]
Ты: Тут все понятно... #З
-
Для чего прибор?

*[Это красивый браслет на руку!]
Ты: Я знаю, это браслет! Его нужно надеть на руку! #Р

Дядя Юрий: Нууу... Вряд ли. #У

Дядя Юрий: Он большеват, чтобы на руке носить. #Н

Дядя Юрий: Да и зачем полицейскому браслет? #П

Стоит подумать еще.
->begaem_po_gorodu.pribor

*[Это переводчик с китайского!]
Ты: Наверное, это переводчик с китайского! Ты его к тексту прикладываешь и он читает! #Р

Дядя Юрий: Было бы здорово, но я не думаю. #Р

Дядя Юрий: В Китай нам пока не за чем. Своих дел полно! #Н

Стоит подумать еще немного.
->begaem_po_gorodu.pribor

*[Это ошейник для собаки!]
Ты: Это ошейник для собаки! Он же идеального размера! #Р

Дядя Юрий: Да... Звучит лочигно. Давай попробуем надеть? #Р
->begaem_po_gorodu.dogina

=dogina
Вы подходите к собачке. Она стояла смирно и не обращала внимание на вас. 

Вы поднесли прибор к ее голове, как вдруг зазвонил телефон. 

Юрий передал прибор тебе и убежал в другой кабинет.

Дядя Юра: Попробуй без меня! Я сейчас вернусь... #У

Вы остались один на один с собакой. И ласково её окликнули.

Наконец, она завиляла хвостом и с интересом посмотрела на обруч. 

Ты решила не терять времени и надеть ошейник. 

Когда он оказался на собаке, она встрепенулась и залаяла! 

Её шерсть встала дыбом! 

Экранчик на ошейнике загорелся и лай прекратился... 

Песик: Что это на мне?! #У

Ты: Ты... Ты... Умеешь разговаривать? #У

Песик: Нет, это вы теперь умеете понимать меня! Как здорово! #Р

Ты: Эээ.. Привет! Я Саша, а тебя как зовут? #Н

Песик: Полицейская собака - рядовой Песик! #Н

Ты: Приятно познакомиться! #Р
->begaem_po_gorodu.pesik_govorit

=pesik_govorit
Что спросить у Песика?

*[Почему Песик себя так вел, когда тут был дядя Юра?]
Ты: Песик, почему когда дядя Юра был тут, ты себя вёл как статуя? Ты его боишься? #Н

Песик: Какой глупый вопрос. #У
Песик: Гав! Начальник Юрий главнее меня. Я должен его слушаться и не мешать! #Н
Песик: Это большая честь служить у него! #Р

Ты: Ну да... Логично. #Н

->begaem_po_gorodu.pesik_govorit

*[Как Песик попал в полицейский участок?]
Ты: Песик, а как ты попал в полицию? Ты тут работаешь? #Н

Песик: Гав! Меня недавно сюда перевели работать! Я - полицейская собака! #Н

->begaem_po_gorodu.pesik_govorit

*[Какая у песика любимая еда?]
Ты: А что ты любишь кушать? #Р

Песик: Я ем то, что дают!.. А вообще... Очень люблю сосиски! #Н
Песик: Жалко тут их нет... #П
-
Песик: Гав! Теперь моя очередь спрашивать! #Н

Песик: Что тебя привело в полицию? Может быть ты ведешь расследование?.. #Н

Песик: Я уже давно без дела сижу... Мне не дают проявить себя! #П

Песик: Как я стану великим собакой-полицейским, если сижу без дела и ничего не расследую? #У

Песик заметно погруснел. 

Ты решаешься посвятить Песика в ход своего расследования. 

Собака слушала, не перебивая. 

Песик: Потрясающе! Если... Если я расследую это дело, то... То стану... #Н
Песик: Возьми меня с собой!  #Р

Ты: Конечно я возьму тебя! Только вот... #Н

Дверь в кабинет открылась и внутрь вбежал дядя Юра.

Дядя Юра: Слушай, Саша. У меня срочное дело! Нужно в другой город ехать! #П

Дядя Юра: Иди домой пока... Точно. Собака! Куда ее?. #У

Ты: Дядя Юра, а можно я на ночь возьму Песика к себе домой? #П

Ты: Я его покормлю, свожу гулять и уложу спа.. #Н

Дядя Юра: Отличная идея! Бери Песика, а я убегаю! Пока! #Р

Дядя выбежал из кабинета, не закрыв даже дверь.
Вы с Песиком посмотрели на друг друга. 
->begaem_po_gorodu

= babushka
#Location: idem
~ v_gorode_proshli = v_gorode_proshli + 1
Определившись с направлением, ты бежишь к дому своей любимой бабули. 

#Location: Dom_baby
Зайдя в нужную пятиэтажку ты улавливаешь приятный запах бабушкиного фирменного пирога.  

Открыв дверь, ты замечаешь свою бабушку разговаривающую на кухне с другой старушкой. 

Бабушка обернулась.

Бабушка Люба: Ох.. Внученька! Я тебя так ждала! #У
Бабушка Люба: Что ты на пороге стоишь? Заходи скорее. Я пирог испекла! #Н

Ты: Привет бабуля! Как ты? #Н

Бабушка обняла тебя.

Бабушка Люба: Иди садись за стол. Я тебя сейчас обедом накормлю! #Н

Бабушка Люба: Вот, кстати, поздоровайся с Ниной Павловной. #Р

Ты: Здравствуйте. #Р

Баба Нина: Здравствуй, Сашенька. Какая ты взрослая! #Р

Без сомнения, Нина Павловна - та самая бабуля из утренних новостей.

Это твой шанс!

Нина Павловна повернулась к бабушке Любе и продолжила свой рассказ. 

Баба Нина: Так вот! Я даже договорить не успела, а репортер уже убежал! #З

Ты: А что же было дальше? Что вы видели? 
-
Баба Нина: А потом... Когда одеяло уже вернулось, я заметила, что оно все мокрое! #Н

Баба Нина: И еще кое-что заметила... Я же раньше парфюмером была! #Р

Баба Нина: Одеяло пропахло рыбой и водорослями. #Н

Возможно, эта информация поможет в расследовании.

Стоит ли вводить бабушек в курс расследования?

*[Лучше промолчать.]
Ты спокойно ела и слушала старушек.

Разговор уходил все дальше от пропажи одеял.

*[Расскажу! Вдруг они смогут помочь?]
~ babuskina_pomosh = 1

Ты начала рассказ, о своих приключениях.

С каждым словом бабушки удивлялись все больше и больше.

После окончания рассказа, они взглянули друг на друга, будто спрашивая о чем-то.

Бабушка Люба: Саша, это правда? #У

Баба Нина: Одела пропали не только у нас... #У

Баба Нина: Саша, это все очень интерсно, но давай подождем, когда взрослые во всем разберутся! #З

Бабушка Люба: Потрясающая история! Скорее бы ночь! #З

Все выглядело очень странно, но кажется, ты сделала правильный выбор.
-
Ты попробовала пирог, обняла бабушку и отпросилась прогуляться.

->begaem_po_gorodu


= port
#Location: idem
~ v_gorode_proshli = v_gorode_proshli + 1

Самое время наведаться к старшему брату в порт! 

Он работает в порту и, наверняка, сможет рассказать что-то дельное.

#Location: Port2
Интуиция подсказывает, что здесь ты сможешь что-то разузнать. 

Слышались отдаленные разговоры, рев моторов и шум моря. 

Спустя несколько минут поисков, ты находишь старшего брата, настраивающего что-то на одной из лодок.

Старший брат: Ооо... Саша привет! Ты что тут делаешь? #Н

Ты: У меня к тебе разговор! #Р

Брат отрывается от работы и поворачивается к тебе.

Старший брат: Давай ближе к делу. Мы скоро отправляемся. #З

Что стоит сказать?

*[Спросить про последнюю ночь!]

Ты: У вас в порту этой ночью ничего странного не происходило? #Н

Старший брат: Ты не представляешь!... Я видел на дне стаю странных белых  рыб. #З

Старший брат: Я пытался хоть одну поймать... Но не вышло. #Н

Послышался голос одного из рыболовов.

Старший брат: Сегодня я хочу устроить реванш!... Хе-хе-хе! #Р

Старшего брата опять позвали.

Старший брат: Иду! Иду! Пока, Саш. #Р

Ты хотела бы расспросить брата поподробнее, но времени не было и ты просто помахала рукой. 

Похоже ночью придется посетить порт!

*[Рассказать о пропаже одеял]
Ты: Сегодня ночью произошло такое! Такое! #Н

Старший брат: Саш, опять твои истории! У меня сейчас совсем нет времени. Давай в выходные зайду в гости. #З

Послышался голос одного из рыбаловов.

Старший брат: Меня зовут! Пока, Саш! #З

В не самом хорошем расположении духа, ты уходишь из порта.
-
->begaem_po_gorodu


===noch2===
#Location: IdemSPsomKBabke

Наконец, ты решаешь вернуться домой. 

Дома ты попрощалась с родителями, вы с Песиком отправились к бабушке. 

Ты: А на каком языке думают собаки? #Н

Песик: Собаки, как я, не думают, а сразу делают! #Р
Песик: В моей работе главное - не упустить возможность! #Н

Ты: Ты хоть понял, что сейчас сказал? #Р

Песик: Нет конечно! #Р

Ты: Кстати, а почему дядя Юра не взял тебя с собой на задание? Ты же полицейская собака! #Н

#Location: VecherDomaUBabushki
Песик: ... #П

21 декабря, 20:00.

Открыв дверь, ты сразу заметила бабушку.
Она стояла в коридоре и пристально смотрела на тебя.

Ты: Привет бабушка! #Н

Бабушка Люба: Привет! Как дела у тебя? Что делала? #Н

Наконец бабушка заметила какое-то большое коричневое существо, которое стояло за твоей спиной...

Бабушка Люба: Саша... Там! СОБАКА?! #У

Ты: Ой... Прости бабушка! Я совсем забыла рассказать. #П

Ты: Это мой новый друг! Песик! #Р

Бабушка Люба: Ты серьезно?! #У

Бабушка Люба: Где ты его взяла?! Он привитый?! #З

Ты: Дядя Юра попросил присмотреть за ним. #З

Бабушка Люба: Ладно. Только на одну ночь. #З

Успокоившись, бабушка пошла готовить ужин. Вы покушали, покормили Песика и, наконец, появилось время отдохнуть.

->noch2.otdihaem

=otdihaem

Что поделаем?

*[Посмотреть фотоальбом!]
#Location: VecherDomaUBabushki
Бабушка достала с полки старый фотоальбом и показала его тебе.

Бабушка Люба: Хочешь посмотрим фотографии? #Р

Не дождавшись ответа, она открыла альбом и показала тебе одну из фотографий.

Бабушка Люба: Смотри! Вот тут ты танцуешь на моем дне рождения! #Н

Бабушка Люба: Жаль, что ты бросила кружок танцев. А ведь у тебя хорошо получалось! #П

Ты: Но мне же нужно было время для того, что действительно нравится! #У

Бабушка Люба: Ты про те твои игры в следователя что-ли? #З

Ты: Ну, бабушка! Это не игры! Я, когда вырасту, стану самой крутой сыщицей! #П

Ты: И сейчас, когда я раскрою пропажу одеял, все поймут, что у меня талант! #З

Песик: Гав! Конечно ты достойна! #Р

Бабушка Люба: Саша... Мне показалось или он сейчас что-то сказал? #У

->noch2.otdihaem

*[Поговорить с Песиком!]
#Location: SPecikomOtosli

Вы с Песиком отошли в другую комнату. Он явно хотел тебе что-то сказать.

Песик: Слушай...Мой нос не врет! Я сегодня сильнее, чем обычно, чувствую запах рыбы... #З

Песик: ... #У

Персик: Не знаю, чем это поможет, но просто хотел, чтобы ты знала.
->noch2.otdihaem

*[Фотография незнакомца!]
#Location: VecherDomaUBabushki
{foto == 1: ->noch2.foto_est }

{foto == 0: ->noch2.foto_net }

->noch2.otdihaem

*[Идем спать!]

Пора пойти спать. Нужно отдохнуть и набраться сил.

->noch2.igra_nachalas

=foto_est
~kamera_zariadka = 1
Ты подбежала к рюкзаку, достала телефон! 

Ты: Песик! Песик! Сюда! #Н

Песик сел рядом и ты нашла фото, которое успела сделать прошлой ночью. Бабушке такое не стоило показывать, чтобы не волновалась. На тёмном фоне отчетливо было видно идущее по дороге...

Песик: Одеяло! Это же одеяло! Гав! #Н

->noch2.otdihaem

=foto_net
Ты подбежала к рюкзачку, достала телефон...

И опять пожалела, что не сфотографировала того монстра, а так хотелось показать его Песику! 

Ты: Поставлю заряжаться. Ночью понадобится! #З

Ты: Надо же улики собирать! #Н
-
->noch2.otdihaem


=igra_nachalas

#Location: BabuskinDomNoch
22 декабря, 00:50.

Тебя разбудило гавканье Песика. 

Песик: Пора вставать! Сейчас самое время...и запах рыбы усилился. #Р

Встав, потянувшись и осмотревшись, ты надела верхнюю одежду, припряианную накануне.

Ты: Ой... Я, кажется, забыла фонарик на кухне! Я сейчас схожу за ним. #У

Стараясь не разубдить бабушку, ты отправилась на кухню. Под ногами поскрипывали половицы.

Открыв кухонную дверь, ты замечаешь бабушку Любу, ищущую что-то в шкафчиках.

Бабушка Люба: Саша, ты что тут делаешь? Ночью! #З

Ты: Бабушка, а ты что тут делаешь? #Р

Бабушка Люба: ... #У

Бабушка Люба: Ты же не собираешься "расследовать" пропажу одеял? #З

Ты: Ну бабушка! #П

Бабушка Люба: Эх... Саша, я не хочу, чтобы ты подвергала себя опасности из-за какой-то игры. #П

Ты: Бабушка, я не маленькая! #П

Бабушка Люба: Я запрещаю тебе... #З

Голос бабушки, заглушил Песик.

Песик: ОНО ИДЕТ! ОНО ИДЕТ!! #З
->noch2.odeialo_uhodit

=odeialo_uhodit

Вы бросились в комнату! Песик сидел в оцепенении, глядя на белое одеяло, взлетающее к потолку!

Одеяло упало на пол и медленно, неестественно встало.

Бабушка Люба: Эта собака реально разговаривает... #У

Одеяло медленно пошло к окну на улицу.

Песик, преодолев страх, бросился на одеяло, пытаясь удержать его!

Он вцепился в белый бок, но одеяло оказалось сильнее! 

Одеяло открыло окно и вместе с прицепившимся Песиком спрыгнуло вниз.

Ты схватила рюкзак, фонарик и запрыгнула на подоконник!

Бабушка Люба: Саша нет! #У

#Location: NochNebo
Ты: Прости бабушка! Мне нужно спасти собаку. #З

Ты выпрыгнула из окна и приземлилась в сугроб. 

Ты: Фух... Как хорошо, что бабушка живет на первом этаже. #У

Ты бросилась за идущим одеялом! С каждым шагом оно шло все быстрее.

Песику становилось все сложнее держаться...

На секунду ослабив хватку, собака шлепнулась в снег. Песик с виноватым видом посмотрел на тебя. 

Ты: Давай скорее! Нельзя его упустить! #З

Захватив собаку, ты продолжаешь погоню! 

Одеяло скрывается за углом. Пытаясь его нагнать, ты выдыхаешься. 

Ты: Он ушел!.. Что теперь делать? #П

Песик: Гав.. Гав.. Я же полицейская собака и смогу найти этого беглеца по запаху! Гав! #Р

Собака сделала большой вдох, а затем побежал по темным улицам.

Начался снегопад. Снежинки летели в лицо и мешали обзору.

#Location: Poterialis
Через минут 5 этого бешеной гонки ты упала в огромный сугроб. 

Кружилась голова, тело утопало в белоснежной массе.

Отдышавшись, ты встала на ноги и огляделась...

Пришло осознание, что ты не знаешь где находишься...

Ты: А где мы?.. Песик ты знаешь? #У

Песик: ... #У

Собака отвела взгляд. Снежная буря усилилась.

Белые хлопья отделяли вас друг от друга.

Ты: Куда ты меня привел?! Где одеяло?.. #П

Песик: ... #П

Ты: Ты потерял след? Почему ты молчишь?! #П

Холод окружил тебя. Песик смотрел на тебя своими щенячими глазами...

И молчал... Просто молчал.

Ты: Я не знаю что делать... Мы заблудились!...  #П

Песик: Помнишь ты спрашивала, почему меня не взяли на задание? #П

Голос песика дрожал. Снежная буря все больше разделяла вас.

Песик: ...Меня не взяли потому, что я не полицейская собака. #П

Песик: Я не могу отслеживать преступников по запаху... #П

Песик: Я не могу решать загадки за считанные минуты... #П

Собака дрожала всем телом.

Песик: Я... Я вообще ничего не могу. #П

Песик: Я... Подвел тебя. #П

Песик: Прости меня пожалуйста! #П

#Location: Poterialis2
Песик: Я не достоин... #П

Прибор для перевода издал странный звук и выключился от холода. 

Собака скрылась в снежном тумане. Ты осталась совсем одна.

Ты: ... #П

Ты: Песик! Стой! #У

Ты бросилась в снежный туман, пытаясь отыскать его.

Ты: Я тоже... Не настоящая сыщица. #П

Ты: Я маленькая...но #П

Ты: если мы будем учиться, то станем... Настоящими полицейскими. #Р

Ты: А вообще, если мы не выберемся, то замерзнем. Так что лучше нам... #З

Песик и бросился к тебе и прижался всем телом.

Ты: Ты плачешь потому, что мы помирились или потому, что мы скоро покроемся инеем? #Р

#Location: NochNebo
Песик: Гав! Гав! #Н

Буря стихла. В одном из сугробов ты заметила небольшой проход!

Ты: Давай выберемся из этой дыры и раскроем дело! #Н

#Location: PortNoch
22 декабря, 01:30.

Старший брат возился с чем-то в порту. 

Вдруг он заметил какую-то странную фигуру на палубе одного из кораблей.

Страший брат: Эй! Ты там! Совсем ночью не спится? #П

Неизвестный: ... #Н

Фигура обернулась. Лица не было видно, если оно было вообще. 

Старший брат: Грохнешься сейчас! Вода сейчас ледяная! Мигом в сосульку превратишься. #П

Неизвестный в пару прыжков оказался перед братом. Медленным шагом он приближался к нему.

Старший брат: Знаешь... Я пойду... хехехе. #У

Фигура бросилась на брата. Посыпались электрические искры! 

Брат вскрикнул от боли, а затем упал без сознания.

Незнакомец: Извини, человек. Не стоило мне мешать. #Н

#Location: NochNebo
Незнакомец вернулся на корабль и отчалил от берега.

Тем временем, вы с Песиком уже бежали по спящему городу.

#Location: OdeialoNochiuIdut
К счастью, вы нашли дорогу и выбежали на знакомую улицу. 

Из домов показывались одеяла. Они медленно шли по улицам, не замечая ничего вокруг.

Песик: Мне кажется или они все идут в одно место?

Ты: В порт! Они идут в порт!! #Н

Вы побежали в том же направлении! 

#Location: PortNoch
Со всего города тысячи одеял маршировали к морю.

Прибежав, вы замечаете лежащего без сознания брата и небольшой корабль отплывающий от берега. 

Ты: Песик! Это же мой старший брат! #З

Корабль остановился. По небу прошла молния.

На пристань спрыгнула темная фигура, напоминающая человека. 
Фигура медленным шагом приближалась к вам.

Незнакомец: Вы не помешаете мне. 
Незнакомец вышел из тени. 

Песик: ...Скат? Серьезно? #У

Скат: Я не хочу делать вам больно. Просто уйдите и не мешайте мне. #Р

Ты:  Это ты убил моего брата?! Ты украл одеяла!? Ты преступник! Так что тебе лучше... #Н

Брат застонал и перевернулся на бок. 

Песик: Саш, твой брат жив! Он приходит в сознание! #Н

Пёсик попытался задержать ската. 

Скат: Плохая собака. Фу! #З

Скат попытался ударить Песика электрическим током. Песик отпрыгнул.

Скат: Девочка, сейчас ты забираешь свою собаку и уходишь. #Р

Скат: Ты уйдешь и забудешь про то, что увидела. #Н

Скат: Или... #З
->noch2.zabrala_psa

=zabrala_psa
Скат: Сами уйдете или вам помочь? #З

#Location: NochNebo
Вас окружили одеяла. Они вытолкали вас с порта, не давая приблизиться к скату. 
Там же остался старший брат. 

Ты: Нам нужно пробраться к брату, а потом на его корабль. #З

Ты: Я сейчас вызову скорую для брата и полицию! Мы задержим Ската до приезда машин! #Р

{nomer == 1: Ты достала телефон, набрала номер и позвонила в полицию.}

{nomer == 1: Ты: Они скоро приедут! Нам нужно задержать ската... #Р}

{nomer == 0: Ты достала телефон и собиралась набрать номер, как вдруг поняла...}

{nomer == 0: Ты: Ой... А какой номер то?.. #У}

{nomer == 0: Песик: Ты серьезно? #У}

{nomer == 0: Ты: Говорила мама, что выучить надо... #П}

{nomer == 0: Ты: Нууу... Тогда попробуем сами? #Р}

Ты: У меня есть план... Песик за мной! #Р

#Location: PortNoch
Вы забрались в одно из одеял и отправились за скатом.

Скат, тем временем, загружал еду на корабль

Скат: Ящик креветок, мешок рыбы, крабы. #Р

Вдруг, он заметил какое-то странное одеяло.

Оно шаталось и падало. Скат подошел к странному одеялу и осмотрел его.

Скат: С тобой все хорошо? Может помочь? #У

Скат: Люди тебя так замучили? #П

Кровь застыла в жилах. Вы стояли внутри этого одеяла.

Скат рассматривал вас несколько секунд, а затем повел на корабль.

Остальные одеяла уже толпились у корабля.

Скат: Тебе, похоже, будет сложно плыть самостоятельно. Бедняжка! #П

Скат: Поплывешь на корабле. Загружайся! #Н

Вы с Песиком ждали нужного момента, чтобы напасть. 

#Location: Plavaiem
Наконец, все было готово и скат запустил двигатель.

Корабль отправился в плаванье, а остальные одеяла поплыли за ним.

Настал ваш черед!

Теперь когда скат занят управлением судна, пора показать ему, кто тут главный!

Вы тихо выбрались из одеяла и встали за спиной Ската.

Ты: 3... 2... 1... #Р

Песик: Сюрприз! #Р

Только скат обернулся, как вы уже набросились на него!

Деревянной доской ты попыталась оглушить Ската. Бац! Начали связывать его. 

Бум! Скат откинул Песика и тот ударился о борт корабля. 

Ай! Скат попытался ударить тебя током, но не дотянулся

Гав! Песик забежал за его спину, пытаясь отвлечь.

{kamera_zariadka == 1: Пам! Ты вспышкой телефона ослепляешь ската!}

{kamera_zariadka == 1: Не зря заряжали!}

Скат вырвался и оказался на носу корабля. 

Сзади - ледяная вода. Спереди - вы.

Скат: У вас ничего не выйдет. Почему вы меня не послушались? #Н

Скат: С вероятностью 98% я одержу победу! #З

Ты: Ты совершил нападение и кражу! Одеяла со всего города плывут за кораблём! Мы должны вернуть все обратно. #З

Скат: Кража? Нет. Я бы назвал это спасением. #Н

Вдруг скат сделал сальто и пропал под водой.

Песик: Эм... Ну все. Мы победили! #Р

Песик: ... #У

Песик: САША, СЗАДИ!! #У

Обернувшись, ты замечаешь летящего прямо на тебя Ската!

БА-БАХ!

Вы сидите связанные рыболовными сетями. 

Скат самодовольно стоит у штурвала.

Скат: Ну что, девочка? Не получилось вернуть скатов себе? #Н

Ты: Какие скаты? Ты о чем вообще? #У

Скат: Вы - люди вылавливаете нас - скатов, чтобы укрываться нами от холода! #З

Скат: Они испытывали жуткую боль, находясь на суше, но сейчас скаты спасены! #П

Скат: Благодаря мне! #Н

Ты: Скаты? Одеяла, это просто кусок ткани. #У

Скат: Просто кусок ткани?! Это не правда! #З

Скат: Ты просто пытаешься обмануть меня! #З

Ты: У одеял нет рта, глаз, жабр. У них вообще ничего нет! #З

Скат: Нет-нет-нет! Не правда! #У

Ты: Это предметы. #З

Скат: ... #У

Скат: О... Нет. Получается я... #У

Скат раскаянно посмотрел на вас, а затем кинулся развязывать.

Скат: Простите меня! Это недоразумение. Бабушка в детстве читала мне сказку про потерявшихся на земле скатов. Я думал, это они. Я вырос в лаборатории и почти ничего не помню о своём детстве и мире скатов, кроме этой сказки. #У

Скат: Я верну все одеяла! А брат твой уже ушел домой. Я использовал слабый заряд тока. #У

Скат нажал несколько кнопок на руке.

Воздух на мгновение будто сжался.

Одеяла перестали двигаться!

{nomer == 1: Послышался звук сирен. Похоже полиция уже близко!}

Только вот... Сдать ли ската полиции?

*[Сдать ската полиции.]
#Location: NochNebo
Вы причалили к берегу, Скат смиренно прошёл в полицейскую машину. 
Приехала бабушка Люба с Бабой Ниной, старший брат и даже дядя Юра. 

Ската отправили в полицейский участок до выяснения обстоятельств.

Ты бросилась в объятия к бабушке и старшему брату.

Ты: Бабушка, прости меня пожалуйста! #П

Бабушка молча обняла тебя, будто говоря, что прощает.

Баба Нина: Саша, ты что учудила тут? #У

Песик весело бегал по кругу. Он радовался закрытому делу, но тебе было не до веселья.

Хоть скат и сказал, что готов понести заслуженное наказание, тебе было не по себе. Ты сочувствовала ему. 

В итоге, в качестве наказания у ската изъяли экзоскелет, который ему сделали в лаборатории для исследований и перемещения по суше. 

Его выпустили на волю, он уплыл к своей семье и, наконец, встретил свою бабушку. .

Дома ты сладко уснула в своей кровати. Рядом посапывал Песик.

А на следующий день вы пошли искать новое преступление. !

->END

*[Отпустить его.]

Ты: Скат, если тебя поймают, то точно накажут. Я верю, что ты не хотел ничего плохого. #П

Ты: Лучше уплывай к своей семье в море. #Н

Скат нажал несколько кнопок и одеяла замерли обездвиженные. 

Скат: К сожалению, я не могу выбраться из моего экзоскелета и уплыть на глубину. #Р

Ты разбила стеклянную часть экзоскелет. 
Скат повредил электронику, чтобы никто больше не смог его использовать, нырнул в воду. 
Он посмотрел на вас из воды, будто прощаясь. 
#Location: NochNebo
Сломанный костюм ты бросила на дно. 

Приехала полиция, бабушка Люба с Бабой Ниной, старший брат и даже дядя Юра! 

Ты бросилась в объятья к бабушке.

Ты: Бабушка, прости меня пожалуйста! #П

Бабушка молча обняла тебя, будто говоря, что прощает.

Баба Нина: Саша, ты что учудила тут? #У

Все, удивились, конечно, что одеяла оказались в море, но посчитали аномалией.

 Утром их выловили из моря и раздали владельцам. Дело было закрыто и больше одеяла по ночам не сбегали. 

Дома ты сразу уснула и видела сны о белых скатах. 
Рядом похрапывал Песик.

А на следующий день вы пошли искать новое преступление!

...
#Location: Port2
23 декабря, 15:30.

У моря прогуливался странного вида мужчина.

Вдруг он заметил что-то блестящее валяющиеся в камнях.

Подойдя ближе он замечает поломанный экзоскелет.

Мужчина нахмурился и записал что-то в свой блокнот.

Мужчина: Кажется кто-то плохо прячет улики... #Н

Мужчина: Почему я разговариваю сам с собой? #Н

Мужчина: Ладно так наверное все делают. #Н

->END

